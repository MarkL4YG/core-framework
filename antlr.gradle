/// ===== ANTLR 4 ===== ///

// BEGIN Fix for Gradle bug #2565
// "cannot find tokens"-bug with packaged grammar
// Found via. https://github.com/gradle/gradle/issues/2565#issuecomment-417916273
// From: https://github.com/apache/groovy/blob/master/subprojects/parser-antlr4/build.gradle#L34
final PARSER_PACKAGE_NAME = 'de.fearnixx.jeak.antlr'

generateGrammarSource {
    outputDirectory = new File('build/generated-src/antlr/main')
    outputs.cacheIf { true }
    arguments += ["-visitor", "-no-listener", "-package", PARSER_PACKAGE_NAME]
}


task fixGrammarSourcePackage(type: Copy) {
    def parserFilePattern = '*'
    def outputPath = generateGrammarSource.outputDirectory
    def parserPackagePath = "${outputPath}/${PARSER_PACKAGE_NAME.replace('.', '/')}"
    doFirst {
        file(parserPackagePath).mkdirs()
    }

    from(outputPath) {
        include parserFilePattern
    }
    into parserPackagePath

    dependsOn tasks.generateGrammarSource
}

task deleteInvalidGrammarSource(type: Delete) {
    def parserFilePattern = '*'
    def outputPath = generateGrammarSource.outputDirectory
    def parserPackagePath = "${outputPath}/${PARSER_PACKAGE_NAME.replace('.', '/')}"
    delete {
        fileTree((File) outputPath) {
            include parserFilePattern
        }
    }

    dependsOn tasks.fixGrammarSourcePackage
}


classes {
    dependsOn tasks.deleteInvalidGrammarSource
}

// END Fix #2565

/// ===== Generic gradle settings ===== ///