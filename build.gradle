group 'de.fearnixx'
project.ext.artifactId = 't3serverbot'
version '0.0.54-bleeding'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url uri('https://maven.fearnixx.de/maven2')
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    api {
        java {
            srcDirs = ['src/api/java']
        }
    }
    dev {
        java {
            srcDirs = ['src/dev/java']
        }
    }

    template {
        java {
            srcDirs = ['src/template/java']
        }
    }
}

dependencies {
    compile group: 'de.mlessmann', name: 'javacommon', version: '2.4.3'
    compile group: 'de.mlessmann', name: 'configurate', version: '1.0.2'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile sourceSets.api.output

    devCompile group: 'de.mlessmann', name: 'javacommon', version: '2.4.3'
    devCompile group: 'de.mlessmann', name: 'configurate', version: '1.0.2'
    devCompile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    devCompile sourceSets.api.output
    devCompile sourceSets.main.output

    templateCompile sourceSets.api.output
    templateCompile group: 'de.mlessmann', name: 'javacommon', version: '2.4.3'
    templateCompile group: 'de.mlessmann', name: 'configurate', version: '1.0.2'
}

compileJava {
    dependsOn tasks.compileApiJava
    source = [sourceSets.api.allJava, sourceSets.main.allJava]
}

compileDevJava {
    dependsOn tasks.compileJava
}

compileTemplateJava {
    dependsOn tasks.compileApiJava
    source = [sourceSets.api.allJava, sourceSets.template.allJava]
}

task apiJar (type: Jar){
    from sourceSets.api.output
    from sourceSets.api.allSource
    classifier 'api'
}

task templateJar (type: Jar) {
    from sourceSets.template.output
    from sourceSets.template.allSource
    classifier 'tpl'
}

jar {
    dependsOn tasks.apiJar
    dependsOn tasks.templateJar
}

task zipPublish(type: Zip){
    from jar
    from 'src/deployment'
    into('libraries') {
        from configurations.runtime
        exclude { dep -> !dep.file.name.endsWith('.jar')}
    }
}

build {
    dependsOn publishToMavenLocal
    dependsOn zipPublish
}

publishing {
    repositories {
        mavenLocal()
        maven {
            url uri('build/maven2')
        }
    }
    publications {
        main(MavenPublication) {
            groupId project.group
            artifactId project.ext.artifactId
            version project.version

            artifact jar
        }
        api(MavenPublication) {
            groupId project.group
            artifactId project.ext.artifactId
            version project.version

            artifact apiJar
        }
        template(MavenPublication) {
            groupId project.group
            artifactId project.ext.artifactId
            version project.version

            artifact templateJar
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}