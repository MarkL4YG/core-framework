group 'de.fearnixx'
def projectVersion = '0.2.1'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8


if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}
version projectVersion

project.ext {
    artifact = 't3serverbot'
    
    hibernateVersion =  '5.3.6.Final'
    confortVersion = '0.2.10-SNAPSHOT'
    log4jVersion = '2.11.1'
}

repositories {
    mavenLocal()
    maven {
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
    }

    main {
        java {
            srcDirs = ['src/api/java', 'src/main/java']
        }
        resources {
            srcDirs = ['src/api/resources', 'src/main/resources']
        }
    }
}

dependencies {
    apiCompile group: 'de.mlessmann', name: 'confort-api', version: project.ext.confortVersion
    apiCompile group: 'org.hibernate', name: 'hibernate-core', version: project.ext.hibernateVersion

    compile group: 'de.mlessmann', name: 'confort', version: project.ext.confortVersion
    compile sourceSets.api.output

    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'org.hibernate', name: 'hibernate-core', version: project.ext.hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-c3p0', version: project.ext.hibernateVersion
    compile files('libraries/mysql-connector-bin.jar')

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtime group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-api', version: project.ext.log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: project.ext.log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: project.ext.log4jVersion
}

task zipPublish(type: Zip){
    from jar
    from 'src/deploy'
    into('libraries') {
        from configurations.runtime
        exclude { dep -> !dep.file.name.endsWith('.jar')}
    }
}

build {
    dependsOn publishToMavenLocal
    dependsOn zipPublish
}

apply from: './artifacts.gradle'
apply from: './publication.gradle'

wrapper {
    gradleVersion '4.2.1'
}