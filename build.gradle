group 'de.fearnixx'
def projectVersion = '1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (project.hasProperty("preRelease")) {
    projectVersion += "-rc" + project.property("preRelease")
} else if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}
version projectVersion

project.ext {
    artifact = 'jeakbot'
    
    confortVersion = '0.3.5-SNAPSHOT'
    log4jVersion = '2.11.1'
    hibernateVersion =  '5.3.6.Final'
    hikariCPVersion = '3.3.1'
    jakartaMailVersion = '1.6.3'

    connectors = [
        mysql: '8.0.13',
        mariadb: '2.1.2'
    ]
}

repositories {
    mavenLocal()
    maven {
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
    }

    main {
        java {
            srcDirs = ['src/api/java', 'src/main/java']
        }
        resources {
            srcDirs = ['src/api/resources', 'src/main/resources']
        }
    }
    
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

dependencies {
    apiCompile group: 'de.mlessmann', name: 'confort-api', version: project.ext.confortVersion
    apiCompile group: 'org.hibernate', name: 'hibernate-core', version: project.ext.hibernateVersion
    apiCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile sourceSets.api.output
    compile group: 'de.mlessmann', name: 'confort', version: project.ext.confortVersion
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'org.hibernate', name: 'hibernate-core', version: project.ext.hibernateVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'com.zaxxer', name: 'HikariCP', version: project.ext.hikariCPVersion
    compile group: 'com.sun.mail', name: 'mailapi', version: project.ext.jakartaMailVersion
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.8.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9'
    compile group: 'org.eclipse.jetty', name: 'jetty-http', version: '9.4.12'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.18.v20190429'

    runtime group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-api', version: project.ext.log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: project.ext.log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: project.ext.log4jVersion
    runtime group: 'mysql', name: 'mysql-connector-java', version: project.ext.connectors.mysql
    runtime group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: project.ext.connectors.mariadb
    runtime group: 'com.sun.mail', name: 'smtp', version: project.ext.jakartaMailVersion
    runtime group: 'com.sun.mail', name: 'dsn', version: project.ext.jakartaMailVersion
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

apply from: './artifacts.gradle'
apply from: './distribution.gradle'
apply from: './publication.gradle'

build {
    dependsOn publishToMavenLocal

    if (!project.version.endsWith('SNAPSHOT')) {
        dependsOn tasks.zipDistribution
        dependsOn tasks.tarDistribution
    }
}

wrapper {
    gradleVersion '4.2.1'
}