group 'de.fearnixx'
def projectVersion = '1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8


if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}
version projectVersion

project.ext {
    artifact = 'jeakbot'
    
    confortVersion = '0.3.5-SNAPSHOT'
    log4jVersion = '2.11.1'
    hibernateVersion =  '5.3.6.Final'
    hikariCPVersion = '3.3.1'

    connectors = [
        mysql: '8.0.13',
        mariadb: '2.1.2'
    ]
}

repositories {
    mavenLocal()
    maven {
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
    }

    main {
        java {
            srcDirs = ['src/api/java', 'src/main/java']
        }
        resources {
            srcDirs = ['src/api/resources', 'src/main/resources']
        }
    }
}

dependencies {
    apiCompile group: 'de.mlessmann', name: 'confort-api', version: project.ext.confortVersion
    apiCompile group: 'org.hibernate', name: 'hibernate-core', version: project.ext.hibernateVersion
    apiCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile sourceSets.api.output
    compile group: 'de.mlessmann', name: 'confort', version: project.ext.confortVersion
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'org.hibernate', name: 'hibernate-core', version: project.ext.hibernateVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'com.zaxxer', name: 'HikariCP', version: project.ext.hikariCPVersion

    runtime group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-api', version: project.ext.log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: project.ext.log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: project.ext.log4jVersion
    runtime group: 'mysql', name: 'mysql-connector-java', version: project.ext.connectors.mysql
    runtime group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: project.ext.connectors.mariadb
}

build {
    dependsOn publishToMavenLocal
    
    if (!project.version.endsWith('SNAPSHOT')) {
        dependsOn zipDistribution
        dependsOn tarDistribution
    }
}

apply from: './artifacts.gradle'
apply from: './distribution.gradle'
apply from: './publication.gradle'

wrapper {
    gradleVersion '4.2.1'
}